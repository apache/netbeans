# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.

########### Syntax Coloring ##################################################################

TOKEN:keyword:("TOKEN")
TOKEN:identifier: ( ["a"-"z" "A"-"Z"] ["a"-"z" "A"-"Z" "0"-"9" "_"]* )
TOKEN:operator: ( 
    ":" | "*" | "?" | "+" | "-" | "[" | "]" | "<" | ">" |
    "^" | "|" | "{" | "}" | "(" | ")" | "," | "=" | ";" | 
    "." | "$"
)
TOKEN:string:( 
    "\"" 
        (  
            [^ "\"" "\\" "\r" "\n"] | 
            ("\\" ["r" "n" "t" "\\" "\'" "\""]) |
            ("\\" "u" ["0"-"9" "a"-"f" "A"-"F"] ["0"-"9" "a"-"f" "A"-"F"] ["0"-"9" "a"-"f" "A"-"F"] ["0"-"9" "a"-"f" "A"-"F"])
        )* 
    "\""
)
TOKEN:string:( 
    "\'" 
        (  
            [^ "\'" "\\" "\r" "\n"] | 
            ("\\" ["r" "n" "t" "\\" "\'" "\""]) |
            ("\\" "u" ["0"-"9" "a"-"f" "A"-"F"] ["0"-"9" "a"-"f" "A"-"F"] ["0"-"9" "a"-"f" "A"-"F"] ["0"-"9" "a"-"f" "A"-"F"])
        )* 
    "\'"
)
TOKEN:number:( ['0'-'9']+ )
TOKEN:comment: ( "#" [^"\n" "\r"]* ["\n" "\r"]+ )
TOKEN:comment: ( "/#" - "#/")
TOKEN:whitespace:( ["\n" "\r" " " "\t"]+ )

SKIP:comment
SKIP:whitespace

S = token S;
S = tokenState S;
S = grammarRule S;
S = command S;
S = ;
                    
token = "TOKEN" ":" tokenName ":" token2;
tokenName = <identifier>;
token2 = regularExpression token3;
token2 = properties;
token3 = ":" state;
token3 = ;
tokenState = state tokenState1;
state = "<" <identifier> ">";
tokenState1 = "{" tokenGroup;
tokenState1 = ":" token;
tokenGroup = tokensInGroup "}";
tokensInGroup = token tokensInGroup;
tokensInGroup = ;
                  
regularExpression = "(" regularExpression1 ")";                    
regularExpression1 = reChoice regularExpression2;
regularExpression2 = "|" reChoice regularExpression2;
regularExpression2 = ;
reChoice = rePart reChoice1;
reChoice1 = rePart reChoice1;
reChoice1 = ;
rePart = <string> [ <identifier, "i"> ] rePartOperatorOrMinus;
rePart = "." rePartOperator;
rePart = reClass rePartOperator;
rePart = "(" regularExpression1 ")" rePartOperator;
rePartOperator = ;
rePartOperator = "+";
rePartOperator = "?";
rePartOperator = "*";
rePartOperatorOrMinus = "-" <string>;
rePartOperatorOrMinus = rePartOperator;
reClass = "[" reInClassNegation reInClass "]";
reInClassNegation = "^";
reInClassNegation = ;
reInClass = <string> reInClassMinus reInClass1;
reInClass1 = <string> reInClassMinus reInClass1;
reInClass1 = ;
reInClassMinus = "-" <string>;
reInClassMinus = ;
                    
grammarRule = grammarRuleNT "=" grRightSide ";";
grammarRuleNT = <identifier>;
grRightSide = grChoice grRightSide1;
grRightSide1 = "|" grChoice grRightSide1;
grRightSide1 = ;
grChoice = lookahead grPart grChoice1;
grChoice = ;
grChoice1 = grPart grChoice1;
grChoice1 = ;
grPart = ntRef grOperator;
ntRef = <identifier>;
grPart = tokenDef grOperator;
grPart = <string> grOperator;
grPart = "[" grRightSide "]";
grPart = "(" grRightSide ")" grOperator;
grOperator = "+";
grOperator = "*";
grOperator = "?";
grOperator = ;
tokenDef = "<" tokenDefName tokenDef1 ">";
tokenDefName = <identifier>;
tokenDef1 = "," <string>;
tokenDef1 = ;
lookahead = "{" grRightSide "}";
lookahead = "{" <number> "}";
lookahead = ;
                    
command = commandName command0;
commandName = <identifier>;
command0 = ":" selector [":" value];
command0 = value;
value = class | <string> | properties | regularExpression;
class = <identifier> class1;
class1 = "." <identifier> class1;
class1 = ;
properties = "{" properties1 "}";
properties1 = property properties1;
properties1 = ;
property = <identifier> ":" propertyValue ";";
propertyValue = <string>;
propertyValue = class;
propertyValue = regularExpression;
selector = selectorPart ("," selectorPart)*;                    
selectorPart = selectorIdentifier selectorPart1;
selectorPart1 = "." selectorIdentifier selectorPart1;
selectorPart1 = ;
selectorIdentifier = <identifier>;



AST {
    removeSimple:"!selector,tokenName,tokenDefName,grammarRuleNT,ntRef,selectorIdentifier";
}

COLOR:commandName: {
    color_name: "keyword";
    default_coloring: "keyword";
    foreground_color:"blue";
}
COLOR:tokenDef: {
    color_name: "token";
    default_coloring: "variable";
    foreground_color:"0x228B22";
}
NAVIGATOR:token: {
    display_name: "<html><b><font color=000099>TOKEN</font></b>: $tokenName$</html>";
}

NAVIGATOR:command: {
    display_name: "<html><b><font color=000099>$keyword$</font></b>: $command0.selector$</html>";
}

COMPLETION:keyword,identifier,operator,comment,string,whitespace: {
    text1: org.netbeans.modules.languages.ext.NBS.completion;
}

COMPLETION:tokenName: {
    text1: "identifier";
    text2: "comment";
    text3: "keyword";
    text4: "string";
    text5: "number";
    text6: "operator";
    text7: "whitespace";
}

COMPLETION:regularExpression: {
    text1: "'";
    description1:"<b>'</b> character or string";
    text2: "\"";
    description2:"<b>\"</b> character or string";
    text3: "?";
    description3:"<b>?</b> repeat once or not at all";
    text4: "+";
    description4:"<b>+</b> repeat once or more time";
    text5: "*";
    description5:"<b>*</b> repeat zero or more time";
    text6: "|";
    description6:"<b>|</b> or";
    text7: "(";
    description7:"<b>(</b> subexpression";
    text8: "[";
    description8:"<b>[</b> set of characters ['a'-z' '_']";
    text9: "[^";
    description9:"<b>[^</b> any character expect [^'\\n']";
}

MARK:ERROR: {
    type:"Error";
    message:"LBL_SYNTAX_ERROR";
}

MARK:error: {
    type:"Error";
    message:"LBL_UNEXPECTED_CHARACTER";
}

SEMANTIC_DECLARATION:grammarRuleNT: {
    name:"$$";
    type:"variable";
}

SEMANTIC_DECLARATION:tokenName: {
    name:"$$";
    type:"variable";
}

SEMANTIC_DECLARATION:command0.selector: {
    name:"$$";
    type:"variable";
    condition: org.netbeans.modules.languages.ext.NBS.tokenComand;
}

SEMANTIC_USAGE:selectorIdentifier: {
    name:"$$";
    type:"variable";
    condition: org.netbeans.modules.languages.ext.NBS.notTokenComand;
    declaration_precedes_usage:"false";
}

SEMANTIC_USAGE:ntRef: {
    name:"$$";
    type:"variable";
    declaration_precedes_usage:"false";
}
SEMANTIC_USAGE:tokenDefName: {
    name:"$$";
    type:"variable";
    declaration_precedes_usage:"false";
}

BUNDLE "org.netbeans.modules.languages.resources.Bundle"
