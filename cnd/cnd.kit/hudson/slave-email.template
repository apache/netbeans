<html>
<head>
<title>Hudson build report</title>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
<style type="text/css">
* { font-family: "Verdana", sans-serif; }
h1 { font-size: 14pt; }
h2 { font-size: 14pt; }
h3 { font-size: 12pt; }
p, ul, li { font-size: 12pt; }
pre { font-family: "Courier New", monospace; font-size: 10pt; }
.changeset { font-size: 9pt; }
.SUCCESS { color: green; }
.UNSTABLE { color: orange; }
.FAILURE { color: red; }
</style>
</head>
<body>

<%
def rootUrl = hudson.model.Hudson.instance.rootUrl
def upstreamProject = hudson.model.Hudson.instance.itemMap.get("cnd-build")

def currBuild = build
def prevBuild = build.previousNotFailedBuild

def currUpstreamBuild = upstreamProject.getBuildByNumber(currBuild.upstreamBuilds.get(upstreamProject))
def prevUpstreamBuild = upstreamProject.getBuildByNumber(prevBuild.upstreamBuilds.get(upstreamProject))
%>

<h1 class="${currBuild.result}">Build Result &mdash; ${currBuild.result}</h1>

<h2>Build Information</h2>
<ul>
<li>Build <a href="${rootUrl}${currBuild.project.url}">${currBuild.project.name}</a>
    #<a href="${rootUrl}${currBuild.url}">${currBuild.number}</a></li>
<li>Upstream build <a href="${rootUrl}${currUpstreamBuild.project.url}">${currUpstreamBuild.project.name}</a>
    #<a href="${rootUrl}${currUpstreamBuild.url}">${currUpstreamBuild.number}</a></li>
<li>Build started at ${build.timestampString2}</li>
<li>Build took ${build.durationString}</li>
</ul>

<%
def buildsSincePrev = []
def changesSincePrev = []

if (prevUpstreamBuild) {
    def it = prevUpstreamBuild
    while (it != currUpstreamBuild) {
        buildsSincePrev << it
        it = it.nextBuild
        changesSincePrev += it.changeSet.items as List
    }
} else {
    def it = currUpstreamBuild
    while (it) {
        buildsSincePrev << it
        changesSincePrev += it.changeSet.items.reverse() as List
        it = it.prevBuild
    }
    buildsSincePrev = buildsSincePrev.reverse()
    changesSincePrev = changesSincePrev.reverse()
}

def currTestResult = currBuild.testResultAction
if (currTestResult) {

    def currFailedTests = currTestResult.failedTests.collect {
        [displayName: "${it.className}.${it.displayName}",
            url: "${rootUrl}${currBuild.url}${currTestResult.urlName}${it.url}",
            age: it.age]
    }
    currFailedTests = currFailedTests.sort { String.format("%06d-", it.age) + it.displayName }
    def currFailedTestNames = new HashSet(currFailedTests.collect { it.displayName })

    def prevTestResult = prevBuild?.testResultAction
    def prevFailedTestNames = new HashSet(
        prevTestResult?.failedTests.collect { "${it.className}.${it.displayName}" } ?: [])

    def fixedTestNames = (prevFailedTestNames - currFailedTestNames).sort()

    def testCountDiff = currTestResult.totalCount - (prevTestResult?.totalCount ?: 0)
    def testCountDiffString = (0 < testCountDiff)? "+" + testCountDiff : String.valueOf(testCountDiff)

    def testFailDiff = currTestResult.failCount - (prevTestResult?.failCount ?: 0)
    def testFailDiffString = (0 < testFailDiff)? "+" + testFailDiff : String.valueOf(testFailDiff)

    def successRate = String.format("%.2f", (currTestResult.totalCount - currTestResult.result.failCount) * 100f / currTestResult.totalCount)
%>

<h2>Test Results</h2>

<ul>
<li>Total test count and delta: ${currTestResult.totalCount} / ${testCountDiffString}</li>
<li>Failure count and delta: ${currTestResult.failCount} / ${testFailDiffString}</li>
<li>Success rate: ${successRate}%</li>
</ul>

<% if (currFailedTests) { %>
<h3>Failing Tests</h3>
<p class="FAILURE">The following tests are failing. Age &lt; 5 means recent regression.</p>
<ul>
<% currFailedTests.each { %>
    <li><a href="${it.url}" class="FAILURE">${it.displayName}</a> (failing for ${it.age} builds)</li>
<% } %>
</ul>
<% } %>

<% if (fixedTestNames) { %>
<h3>Fixed Tests</h3>
<p class="SUCCESS">The following tests are no longer failing. Good work!</p>
<ul>
<% fixedTestNames.each { %>
    <li class="SUCCESS">$it</li>
<% } %>
</ul>
<% } %>

<% } %>


<% if (changesSincePrev) { %>
<h2>Changes since last successful build (${changesSincePrev.size()})</h2>
<ul>
<% changesSincePrev.each { change -> %>
<li>
<p><a href="http://hg.netbeans.org/cnd-main/rev/${change.shortNode}">${change.shortNode}</a> by ${change.author}</p>
<pre>${change.msg}</pre>
<ul class="changeset">
<% change.affectedFiles.eachWithIndex { file, idx -> if (idx < 20) { %>
    <li class="changeset">${file.editType.name} ${file.path}</li>
<% } } %>
<% if (20 < change.affectedFiles.size) { %>
    <li class="changeset">${change.affectedFiles.size - 20} more ...</li>
<% } %>
</ul>
</li>
<% } %>
</ul>
<% } %>

<% if (currBuild.result != hudson.model.Result.SUCCESS) { %>
<h2>Build Log</h2>
<pre>${currBuild.getLog(100).join("\n")}</pre>
<% } %>

</body>
</html>
